// source:
// https://github.com/mrvux/dx11-vvvv-girlpower/blob/master/nodes/texture11/Source/Palette.tfx

[TextureSource]
[Category("Source")]
[Tags("hsl hsv")]
[Summary("Returns a color palette texture")]
[DontConvertToSRgbOnWrite]
shader ColorPalette_TextureFX : TextureFX
{
    float4x4 Transform;
    [EnumType("VL.Stride.TextureFX.PaletteType, VL.Stride.TextureFX")]
    int Type = 0;

    float4 HSL(float4 col, float2 uv) 
    {
        col.rgb = HSVtoRGB(float3(uv.x,1,saturate(2-2*uv.y)));
        col.rgb = lerp(col.rgb, max(col.r, max(col.g, col.b)), saturate(1 - uv.y*2));

        return col;
    }

    float4 HSV(float4 col, float2 uv) 
    {
        col.rgb = HSVtoRGB(float3(uv.x, 1, 1-uv.y));

        return col;
    }

    float4 Radial(float4 col, float2 uv) 
    {
        col.rgb = HSLtoRGB(float3(atan2(uv.y-.5, uv.x-.5)/acos(-1)/2, 1, length(uv-.5)));

        return col;
    }
  
    stage override float4 Shading()
    {
        float2 uv = streams.TexCoord;

        uv = mul(float4((uv.xy*2-1)*float2(1,-1)*.5,0,1), Transform).xy*float2(1,-1) + 0.5;
        
        float4 col = 1;

        switch((uint)Type % 3)
        {
            case 0 : col = HSL(col, uv); break;
            case 1 : col = HSV(col, uv); break;
            case 2 : col = Radial(col, uv); break;
        }

        return col;
    }
};