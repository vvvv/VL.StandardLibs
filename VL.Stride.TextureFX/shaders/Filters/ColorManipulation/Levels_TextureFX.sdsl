[Category("Filter")]
[Summary("")]
shader Levels_TextureFX : FilterBase
{
    [EnumType("VL.Stride.TextureFX.LevelsClampType, VL.Stride.TextureFX")]
    int Type;

    [Color] 
    float4 fromBlack = float4(0,0,0,1);
    
    [Color] 
    float4 fromWhite = float4(1,1,1,1);
    
    [Color] 
    float4 toBlack = float4(0,0,0,1);
    
    [Color] 
    float4 toWhite = float4(1,1,1,1);

    float Gamma = 1.0f;

    float4 ClampNone(float4 col)
    {
        return col;
    }

    float4 ClampTop(float4 col)
    {
        return min(col,1);
    }

    float4 ClampBottom(float4 col)
    {
        return max(col,0);
    }

    float4 ClampBoth(float4 col)
    {
        return saturate(col);
    }

    float4 Filter(float4 tex0col)
    {
        float4 c = tex0col;
        float4 fromDelta = fromWhite - fromBlack;
        float4 cMin = c - fromBlack;
        c = cMin / fromDelta;
        c = isnan(c) ? cMin : c;
        c = sign(c) * pow(abs(c), Gamma);
        
        switch (Type)
        {
            case 0 : c = ClampNone(c); break;
            case 1 : c = ClampTop(c); break;
            case 2 : c = ClampBottom(c); break;
            case 3 : c = ClampBoth(c); break;
        }

        c = c * (toWhite - toBlack) + toBlack;
        return c;
    }
};